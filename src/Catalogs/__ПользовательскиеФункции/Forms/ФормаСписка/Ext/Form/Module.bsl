
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Стандартные действия при создании на сервере.
	__РаботаСДиалогамиСервер.СписокПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтотОбъект);
	
	МетаданныеОбъекта = Метаданные.Справочники.__ПользовательскиеФункции;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ПоказатьОписаниеФункции", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура формирует описание функции или группы функций.
// 
// Параметры:
// 	Ссылка - СправочникСсылка.__ПользовательскиеФункции.
// 
&НаСервере
Процедура ПоказатьОписаниеФункцииСервер(Ссылка)
	
	Если Ссылка.ЭтоГруппа Тогда
		
		ТекстОписания = НСтр("ru='#В группе: ';en='#In Group:'") + Символы.ПС;
		
		Выборка = Справочники.__ПользовательскиеФункции.Выбрать(Ссылка);
		
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.ЭтоГруппа Тогда
				Продолжить;
			КонецЕсли;
			
			ОписаниеСКомментариями = СтрЗаменить(СокрЛП(Выборка.Описание), Символы.ПС, Символы.ПС + "// ");
			Если СокрЛП(Выборка.Описание) = "" Тогда
				ОписаниеФункции = НСтр("ru='// <нет описания>';en='// <No description>'");
			Иначе	
				ОписаниеФункции = СтрШаблон("// %1", ОписаниеСКомментариями);
			КонецЕсли;
			
			__ОбщегоНазначенияСервер.ДобавитьСтрокуКТексту(ТекстОписания, ОписаниеФункции);
			СтруктураЗаголовка = Справочники.__ПользовательскиеФункции.ПолучитьНазваниеПодпрограммыСПараметрами(Выборка.Ссылка);
			__ОбщегоНазначенияСервер.ДобавитьСтрокуКТексту(ТекстОписания, СтруктураЗаголовка.ЗаголовокФункции + Символы.ПС);
		КонецЦикла;
		
		ПолеОписания = ТекстОписания;
	Иначе
		СтруктураЗаголовка = Справочники.__ПользовательскиеФункции.ПолучитьНазваниеПодпрограммыСПараметрами(Ссылка);
		
		Элементы.ДекорацияНазваниеСПараметрами.Заголовок = СтруктураЗаголовка.НазваниеСПараметрами;
		ПолеОписания = СтруктураЗаголовка.ОписаниеПараметров + Символы.ПС + СокрЛП(Ссылка.КодПодпрограммы);
	КонецЕсли;
	
	Элементы.Описание.Видимость = Не Ссылка.ЭтоГруппа;
	Элементы.ГруппаШапкаФункции.Видимость = Не Ссылка.ЭтоГруппа;
	Элементы.ГруппаПодвалФункции.Видимость = Не Ссылка.ЭтоГруппа;
	
КонецПроцедуры

// Процедура обработчик ожидания активизации строки.
// 
&НаКлиенте
Процедура ПоказатьОписаниеФункции()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьОписаниеФункцииСервер(ТекущиеДанные.Ссылка);
	
КонецПроцедуры

#КонецОбласти

