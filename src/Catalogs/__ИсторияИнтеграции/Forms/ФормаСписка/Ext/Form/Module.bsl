#Область ОбработчикиКомандФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РегламентноеЗадание				= ПолучитьРегламентноеЗадание();
	РасписаниеРегламентногоЗадания	= РегламентноеЗадание.Расписание;
	ИспользоватьРегламентноеЗадание	= РегламентноеЗадание.Использование;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	НастроитьДоступностьСсылкиНастройкиРасписания();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьРегламентноеЗаданиеПриИзменении(Элемент)
	
	НастроитьДоступностьСсылкиНастройкиРасписания();
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьРасписаниеРегламентногоЗадания(Команда)
	
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(РасписаниеРегламентногоЗадания);
	ОписаниеОповещения = Новый ОписаниеОповещения("РедактированиеРасписанияРегламентногоЗаданияЗавершение", ЭтотОбъект);
	Диалог.Показать(ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

#Область ВспомогательныеПроцедурыИФункции

&НаСервере
Функция ПолучитьРегламентноеЗадание()
	
	МассивЗаданий = РегламентныеЗадания.ПолучитьРегламентныеЗадания(
		Новый Структура("Ключ, Метаданные",
			"__ОчисткаИсторииИнтеграции", Метаданные.РегламентныеЗадания.__ОчисткаИсторииИнтеграции));
		
	Если МассивЗаданий.Количество() = 0 Тогда
		РегЗадание = РегламентныеЗадания.СоздатьРегламентноеЗадание(Метаданные.РегламентныеЗадания.__ОчисткаИсторииИнтеграции);
		РегЗадание.Ключ				= "__ОчисткаИсторииИнтеграции";
		РегЗадание.Использование	= Истина;
		РегЗадание.Расписание		= Метаданные.РегламентныеЗадания.__ОчисткаИсторииИнтеграции.Расписание;
		РегЗадание.Записать();
	Иначе
		РегЗадание = МассивЗаданий[0];
	КонецЕсли;
	
	Возврат РегЗадание;
	
КонецФункции

&НаКлиенте
Процедура НастроитьДоступностьСсылкиНастройкиРасписания()
	
	Элементы.НастроитьРасписаниеРегламентногоЗадания.Доступность = ИспользоватьРегламентноеЗадание;
	
КонецПроцедуры

&НаКлиенте
Процедура РедактированиеРасписанияРегламентногоЗаданияЗавершение(Расписание, ДополнительныеПараметры) Экспорт
	
	Если Расписание <> Неопределено Тогда
		РасписаниеРегламентногоЗадания = Расписание;
		ЗаписатьРегламентноеЗаданиеПослеИзмененияРасписания();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьРегламентноеЗаданиеПослеИзмененияРасписания()
	
	РегламентноеЗадание					= ПолучитьРегламентноеЗадание();
	РегламентноеЗадание.Расписание		= РасписаниеРегламентногоЗадания;
	РегламентноеЗадание.Использование	= ИспользоватьРегламентноеЗадание;
	РегламентноеЗадание.Записать();
	
КонецПроцедуры

#КонецОбласти // ВспомогательныеПроцедурыИФункции
